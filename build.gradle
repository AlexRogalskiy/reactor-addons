import org.gradle.util.VersionNumber
import java.text.SimpleDateFormat

/*
 * Copyright (c) 2011-2021 VMware Inc. or its affiliates, All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
	// we define kotlin version for benefit of both adapter and extra (see kotlin-gradle-plugin below)
	ext.kotlinVersion = '1.5.31'
	repositories {
		mavenCentral()
		maven { url "https://repo.spring.io/plugins-release-local" }
	}
	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
		classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.24.18" //applied in individual submodules
	}
}

plugins {
	id 'biz.aQute.bnd.builder' version '5.3.0' apply false
}

description = 'Reactor Core Addons including processors, adapters and more'

ext {
  //NOTE: reactor-core version is declared in gradle.properties

  // Logging
  slf4jVersion = '1.7.32'
  logbackVersion = '1.2.5'

  // Libraries
  rxJava2Version = '2.2.8'
  rxJava3Version = '3.0.0'
  akkaActorVersion = '2.4.10'

  swtVersion = '4.5.2'
  swtVersionPlatform = '4.5.2'

  // Testing
  assertJVersion = '3.21.0'
  quickTheoriesVersion = '0.25'
  mockitoVersion = '3.12.4'

	//javadoc for JDK detection
	jdk = JavaVersion.current().majorVersion
	jdkJavadoc = "https://docs.oracle.com/javase/$jdk/docs/api/"
	if (JavaVersion.current().isJava11Compatible()) {
		jdkJavadoc = "https://docs.oracle.com/en/java/javase/$jdk/docs/api/"
	}
	println "JDK Javadoc link for this build is ${rootProject.jdkJavadoc}"

	//OSGI version number
	versionNumber = VersionNumber.parse(version.toString())
	if (versionNumber.qualifier == null || versionNumber.qualifier.size() == 0) {
		osgiVersion = "${version}.RELEASE"
		println "$version is a release, will use $osgiVersion for bnd"
	}
	else if (versionNumber.qualifier.equalsIgnoreCase("SNAPSHOT")) {
		def sdf = new SimpleDateFormat("yyyyMMddHHmm");
		sdf.setTimeZone(TimeZone.getTimeZone("UTC"));
		def buildTimestamp = sdf.format(new Date())
		osgiVersion = "${versionNumber.major}.${versionNumber.minor}.${versionNumber.micro}.BUILD-$buildTimestamp"
		println "$version is a snapshot, will use $osgiVersion for bnd"
	}
	else {
		osgiVersion = "${versionNumber.major}.${versionNumber.minor}.${versionNumber.micro}.${versionNumber.qualifier}"
		println "$version is neither release nor snapshot, will use $osgiVersion for bnd"
	}
}

apply from: "$gradleScriptDir/releaser.gradle"

configurations.all {
  // check for updates every build
  resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}


configure(subprojects) { project ->
  group = 'io.projectreactor.addons'

  apply plugin: 'java-library'
  apply plugin: 'kotlin'
  apply from: "${rootDir}/gradle/setup.gradle"
	apply from: "${rootDir}/gradle/javadoc.gradle"

	[compileJava, compileTestJava]*.options*.compilerArgs = ["-Xlint:varargs",
														   "-Xlint:cast",
														   "-Xlint:classfile",
														   "-Xlint:dep-ann",
														   "-Xlint:divzero",
														   "-Xlint:empty",
														   "-Xlint:finally",
														   "-Xlint:overrides",
														   "-Xlint:path",
														   "-Xlint:processing",
														   "-Xlint:static",
														   "-Xlint:try",
														   "-Xlint:deprecation",
														   "-Xlint:unchecked",
														   "-Xlint:-serial",      // intentionally disabled
														   "-Xlint:-options",     // intentionally disabled
														   "-Xlint:-fallthrough", // intentionally disabled
														   "-Xlint:-rawtypes"     // TODO enable and fix warnings
  ]

  compileJava {
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
  }

  compileTestJava {
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
  }

  compileKotlin {
	kotlinOptions.jvmTarget = "1.8"
	kotlinOptions.freeCompilerArgs = ["-Xjsr305=strict"]
  }

  compileTestKotlin {
	kotlinOptions.jvmTarget = "1.8"
	kotlinOptions.freeCompilerArgs = ["-Xjsr305=strict"]
  }

  // now that kotlin-gradle-plugin 1.1.60 is out with fix for https://youtrack.jetbrains.com/issue/KT-17564
  // be wary and fail if the issue of source file duplication in jar comes up again
  sourcesJar {
	duplicatesStrategy = DuplicatesStrategy.FAIL
  }

  if (JavaVersion.current().isJava8Compatible()) {
	compileTestJava.options.compilerArgs += "-parameters"
	tasks.withType(Javadoc) {
	  options.addStringOption('Xdoclint:none', '-quiet')
	}
  }

  [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

  sourceSets.test.resources.srcDirs = ["src/test/resources", "src/test/java"]

  project.tasks.withType(Test).all {
	scanForTestClasses = false
	include '**/*Tests.*'
	include '**/*Test.*'
	include '**/*Spec.*'
	exclude '**/*Abstract*.*'
	exclude '**/dummy/Test.*'
  }

	repositories {
		mavenCentral()
		if (version.endsWith('-SNAPSHOT')) {
			mavenLocal()
			maven { url 'https://repo.spring.io/snapshot' }
		}
		maven { url 'https://repo.spring.io/milestone' }
	}

  // dependencies that are common across all java projects
  dependencies {
	api "io.projectreactor:reactor-core:$reactorCoreVersion"

	// JSR-305 annotations
	compileOnly "com.google.code.findbugs:jsr305:3.0.2"

	// Logging
	compileOnly "org.slf4j:slf4j-api:$slf4jVersion"

	// Testing
	testImplementation 'junit:junit:4.12',
			"org.hamcrest:hamcrest-library:1.3",
			"org.assertj:assertj-core:$assertJVersion",
			"org.quicktheories:quicktheories:$quickTheoriesVersion",
			"org.testng:testng:6.8.5"
	testRuntimeOnly "ch.qos.logback:logback-classic:$logbackVersion"
	testImplementation "io.projectreactor:reactor-test:$reactorCoreVersion"

  }
}

project('reactor-adapter') {
  description = 'Adapters on top of various async boundary providers'

	apply plugin: 'biz.aQute.bnd.builder'

	ext {
		bndOptions = [
			"-exportcontents": "reactor.*;version=$osgiVersion;-noimport:=true",
			"Import-Package": [
				"!*internal*",
				"!javax.annotation",
				"kotlin.*;resolution:=optional",
				"*"
			].join(","),
			"Bundle-Name" : "reactor-adapter",
			"Bundle-SymbolicName" : "io.projectreactor.addons.reactor-adapter",
			"Bundle-Version" : "$osgiVersion"
		]
	}

	dependencies {
		//Optional RxJava 2 Converter
		compileOnly "io.reactivex.rxjava2:rxjava:$rxJava2Version"
		//Optional RxJava 3 Converter
		compileOnly "io.reactivex.rxjava3:rxjava:$rxJava3Version"

		compileOnly "com.typesafe.akka:akka-actor_2.11:$akkaActorVersion"
		compileOnly "org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}"

		testImplementation "org.reactivestreams:reactive-streams-tck:1.0.3"
	}

	jar {
		manifest {
			attributes('Automatic-Module-Name': 'reactor.adapter')
		}
		bnd(bndOptions)
	}

}

project('reactor-extra') {
  description = 'Reactor Extra utilities'

	apply plugin: "biz.aQute.bnd.builder"

	repositories {
		maven { url "https://maven-eclipse.github.io/maven" }
	}

	ext {
		bndOptions = [
			"-exportcontents" : "reactor.*;version=$osgiVersion;-noimport:=true",
			"Import-Package": [
				"!*internal*",
				"!javax.annotation",
				"kotlin.*;resolution:=optional",
				"org.eclipse.*;resolution:=optional",
				"javax.swing.*;resolution:=optional",
				"*"
			].join(","),
			"Bundle-Name" : "reactor-extra",
			"Bundle-SymbolicName" : "io.projectreactor.addons.reactor-extra",
			"Bundle-Version" : "$osgiVersion"
		]
	}

	dependencies {
		compileOnly "org.eclipse.swt:org.eclipse.swt.${getSwtPlatform()}:${swtVersionPlatform}"
		compileOnly("org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}")
		testImplementation "org.reactivestreams:reactive-streams-tck:1.0.3"
		testImplementation "org.mockito:mockito-core:2.23.0"
	}

	jar {
		manifest {
			attributes('Automatic-Module-Name': 'reactor.extra')
		}
		bnd(bndOptions)
	}
}

static def getSwtPlatform() {
  String osname = System.properties['os.name']
  String osarch = System.properties['os.arch']
  if (osname.toLowerCase(Locale.ROOT).contains('win')) {
	if (osarch.contains("64")) {
	  return "win32.win32.x86_64"
	}
	return "win32.win32.x86"
  }
  if (osname.toLowerCase(Locale.ROOT).contains("nux")) {
	if (osarch.contains("64")) {
	  return "gtk.linux.x86_64"
	}
	return "gtk.linux.x86"
  }
  if (osname.toLowerCase(Locale.ROOT).contains("mac")) {
	return "cocoa.macosx.x86_64"
  }
  throw new RuntimeException("Unknown OS/Arch: " + osname + ", " + osarch)
}
